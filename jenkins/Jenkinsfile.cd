pipeline {
  agent any

  options {
    disableConcurrentBuilds()
    timestamps()
  }

  environment {
    AWS_REGION     = "us-east-1"
    AWS_ACCOUNT_ID = "058264153265"
    ECR_REPO       = "personal/gatekeeper"

    CLUSTER_NAME = "gatekeeper-cluster"
    SERVICE_NAME = "gatekeeper-service"
    TASK_FAMILY  = "gatekeeper-task"
  }

  stages {

    stage("Checkout") {
      steps {
        checkout scm
      }
    }

    stage("Resolve Image Tag") {
      steps {
        script {
          def imageTag = sh(
            script: "node -p \"require('./package.json').version\"",
            returnStdout: true
          ).trim()

          if (!imageTag) {
            error("package.json version not found")
          }

          env.IMAGE_URI = "${AWS_ACCOUNT_ID}.dkr.ecr.${AWS_REGION}.amazonaws.com/${ECR_REPO}:${imageTag}"

          echo "Deploying image: ${env.IMAGE_URI}"
        }
      }
    }

    stage("Fetch Task Definition") {
      steps {
        sh """
          aws ecs describe-task-definition \
            --task-definition ${TASK_FAMILY} \
            --region ${AWS_REGION} \
            > task-definition-full.json
        """
      }
    }

    stage("Register New Revision") {
      steps {
        script {
          def revisionArn = sh(
            script: """
              cat task-definition-full.json | jq \
                --arg IMAGE "${env.IMAGE_URI}" \
                '.taskDefinition
                 | .containerDefinitions[0].image = \$IMAGE
                 | del(.taskDefinitionArn)
                 | del(.revision)
                 | del(.status)
                 | del(.requiresAttributes)
                 | del(.compatibilities)
                 | del(.registeredAt)
                 | del(.registeredBy)' \
              > new-task-def.json

              aws ecs register-task-definition \
                --cli-input-json file://new-task-def.json \
                --region ${AWS_REGION} \
                --query 'taskDefinition.taskDefinitionArn' \
                --output text
            """,
            returnStdout: true
          ).trim()

          env.NEW_TASK_DEF_ARN = revisionArn
          echo "Registered new revision: ${env.NEW_TASK_DEF_ARN}"
        }
      }
    }

    stage("Deploy") {
      steps {
        sh """
          aws ecs update-service \
            --cluster ${CLUSTER_NAME} \
            --service ${SERVICE_NAME} \
            --task-definition ${env.NEW_TASK_DEF_ARN} \
            --region ${AWS_REGION}
        """
      }
    }

    stage("Wait For Stability") {
      steps {
        sh """
          aws ecs wait services-stable \
            --cluster ${CLUSTER_NAME} \
            --services ${SERVICE_NAME} \
            --region ${AWS_REGION}
        """
      }
    }
  }

  post {
    success {
      echo "✅ Deployment completed successfully."
    }
    failure {
      echo "❌ Deployment failed."
    }
    always {
      cleanWs()
    }
  }
}
